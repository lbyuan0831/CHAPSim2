
module bc_convective_outlet_mod
  use parameters_constant_mod
  use decomp_2d
  use udf_type_mod
  implicit none 

  private :: get_convective_outlet_velocity
  private :: calculate_fbcx_convective_outlet
  private :: calculate_fbcz_convective_outlet
  !public  :: update_flow_from_dyn_fbcx
  private  :: enforce_domain_mass_balance_dyn_fbc
  !private :: enforce_domain_energy_balance_dyn_fbc
  private :: update_fbcx_convective_outlet_flow
  private :: update_fbcz_convective_outlet_flow
  private :: update_fbcx_convective_outlet_thermo
  private :: update_fbcz_convective_outlet_thermo
  public  :: update_convective_outlet_thermo
  public  :: update_convective_outlet_flow

  contains

!==========================================================================================================
  subroutine get_convective_outlet_velocity(dm, uxdx)
    use wtformat_mod
    use math_mod
    use print_msg_mod
    implicit none
    ! arguments
    type(t_domain), intent(in) :: dm
    real(WP), intent(out) :: uxdx
    ! local variables
    real(WP) :: uxmax, uxmin, uxmax_work, uxmin_work, uintf, dx
    integer :: i, j, k
    integer :: nx, ny, nz

    ! conditions
    if(.not. dm%is_conv_outlet(1) .and. &
       .not. dm%is_conv_outlet(3)) return
    if(dm%is_conv_outlet(1) .and. &
       dm%is_conv_outlet(3)) call Print_error_msg("Not supported.")

    uxmax = MINP
    uxmin = MAXP
    if(dm%is_conv_outlet(1)) then
      ! x - convective velocity
      ny = size(dm%fbcx_qx, 2)
      nz = size(dm%fbcx_qx, 3)
      !$acc parallel loop collapse(2) reduction(max:uxmax) reduction(min:uxmin) &
      !$acc&                          default(present) private(uintf)
      do k = 1, nz
        do j = 1, ny
          uintf = dm%fbcx_qx(2, j, k)
          uintf = abs_wp(uintf)
          if(uintf > uxmax) uxmax = uintf
          if(uintf < uxmin) uxmin = uintf
        end do
      end do
      !$acc end parallel loop
      dx = dm%h1r(1)
    else if(dm%is_conv_outlet(3)) then
      ! y - convective velocity
      nx = size(dm%fbcz_qz, 1)
      ny = size(dm%fbcz_qz, 2)
      !$acc parallel loop collapse(2) reduction(max:uxmax) reduction(min:uxmin) &
      !$acc&                          default(present) private(uintf)
      do j = 1, ny
        do i = 1, nx
          uintf = dm%fbcz_qz(i, j, 2)
          uintf = abs_wp(uintf)
          if(uintf > uxmax) uxmax = uintf
          if(uintf < uxmin) uxmin = uintf
        end do
      end do
      !$acc end parallel loop
      dx = dm%h1r(3)
    else
      call Print_error_msg("Not supported.")
    end if
    ! find the global max/min
    call MPI_ALLREDUCE(uxmax, uxmax_work, 1, MPI_REAL_WP, MPI_MAX, MPI_COMM_WORLD, ierror)
    call MPI_ALLREDUCE(uxmin, uxmin_work, 1, MPI_REAL_WP, MPI_MIN, MPI_COMM_WORLD, ierror)
    ! calc convective velocity
    uxdx = HALF * (uxmax_work + uxmin_work)
    uxdx = uxdx * dx
#ifdef DEBUG_STEPS 
    if(nrank == 0) write(*, '(10X, A, 3ES13.5)') 'convective outlet velocity Max., Min., Ave = ', &
      uxmax_work, uxmin_work, HALF * (uxmax_work + uxmin_work)
#endif

    return
  end subroutine
!==========================================================================================================
  subroutine calculate_fbcx_convective_outlet(fbcx_var, uxdx, fbc_rhs0, var_xpencil, dm, isub)
    ! all based on x pencil
    ! dphi/dt + ux * dphi/dx = 0
    ! data storage:
    ! qx,  -----|-----||-----|
    !          qx     bc2   bc4 
    ! qy,  --x--|--x--||--x--|
    !       qy    qy  bc2 bc4
    ! 
    implicit none
    ! arguments
    type(t_domain), intent(in) :: dm
    real(WP), dimension(:, :, :), intent(inout), contiguous :: fbcx_var
    real(WP), dimension(:, :),    intent(inout), contiguous :: fbc_rhs0
    real(WP), dimension(:, :),    intent(in),    contiguous :: var_xpencil
    real(WP), intent(in) :: uxdx
    integer, intent(in)  :: isub

    ! local variables
    integer :: j, k
    integer :: ny, nz
    real(WP) :: rhs_explicit_current, rhs_explicit_last, rhs_total

    if(.not. dm%is_conv_outlet(1)) return

    ny = size(fbcx_var, 2); nz = size(fbcx_var, 3)
    !$acc parallel loop collapse(2) default(present) &
    !$acc&         private(rhs_explicit_current, rhs_explicit_last, rhs_total)
    do k = 1, nz
      do j = 1, ny
      ! add explicit terms : convection rhs
        rhs_explicit_current = fbcx_var(4, j, k) - var_xpencil(j, k) ! at cell centre for ux, and bc point for otherse
        rhs_explicit_current = - rhs_explicit_current * uxdx
        rhs_explicit_last    = fbc_rhs0(j, k)
        rhs_total = ( dm%tGamma(isub) * rhs_explicit_current + &
                      dm%tZeta (isub) * rhs_explicit_last ) * dm%dt
        fbc_rhs0(j, k) = rhs_explicit_current
      ! calculate updated b.c. values
        fbcx_var(2, j, k) = fbcx_var(2, j, k) + rhs_total
        fbcx_var(4, j, k) = TWO * fbcx_var(2, j, k) - var_xpencil(j, k)
      end do
    end do
    !$acc end parallel loop

    return
  end subroutine 
!==========================================================================================================
  subroutine calculate_fbcz_convective_outlet(fbcz_var, uzdz, fbc_rhs0, var2d_zpencil, dm, isub)
    ! all based on z pencil
    ! dphi/dt + ux * dphi/dx = 0
    ! data storage:
    ! qz,  -----|-----||-----|
    !          qz     bc2   bc4 
    ! qy,  --x--|--x--||--x--|
    !       qy    qy  bc2 bc4
    ! 
    implicit none
    ! arguments
    type(t_domain), intent(in) :: dm
    real(WP), dimension(:, :, :), intent(inout), contiguous :: fbcz_var
    real(WP), dimension(:, :),    intent(inout), contiguous :: fbc_rhs0
    real(WP), dimension(:, :),    intent(in),    contiguous :: var2d_zpencil
    real(WP), intent(in) :: uzdz
    integer, intent(in)  :: isub
    ! local variables
    integer :: i, j
    integer :: nx, ny
    real(WP) :: rhs_explicit_current, rhs_explicit_last, rhs_total

    if(.not. dm%is_conv_outlet(3)) return

    nx = size(fbcz_var, 1); ny = size(fbcz_var, 2)
    !$acc parallel loop collapse(2) default(present) &
    !$acc&         private(rhs_explicit_current, rhs_explicit_last, rhs_total)
    do j = 1, ny
      do i = 1, nx
      ! add explicit terms : convection rhs
        rhs_explicit_current = fbcz_var(i, j, 4) - var2d_zpencil(i, j) ! at cell centre for ux, and bc point for otherse
        rhs_explicit_current = - rhs_explicit_current * uzdz
        rhs_explicit_last    = fbc_rhs0(i, j)
        rhs_total = ( dm%tGamma(isub) * rhs_explicit_current + &
                      dm%tZeta (isub) * rhs_explicit_last ) * dm%dt
        fbc_rhs0(i, j) = rhs_explicit_current
      ! calculate updated b.c. values
        fbcz_var(i, j, 2) = fbcz_var(i, j, 2) + rhs_total
        fbcz_var(i, j, 4) = TWO * fbcz_var(i, j, 2) - var2d_zpencil(i, j)
      end do
    end do
    !$acc end parallel loop

    return
  end subroutine
!==========================================================================================================
  ! subroutine update_dyn_fbcx_from_flow(dm, ux, uy, uz, fbcx1, fbcx2, fbcx3)
  !   use print_msg_mod
  !   implicit none 
  !   type(t_domain), intent(in) :: dm
  !   real(WP), dimension(dm%dpcc%xsz(1), dm%dpcc%xsz(2), dm%dpcc%xsz(3)), intent (in) :: ux
  !   real(WP), dimension(dm%dcpc%xsz(1), dm%dcpc%xsz(2), dm%dcpc%xsz(3)), intent (in) :: uy
  !   real(WP), dimension(dm%dccp%xsz(1), dm%dccp%xsz(2), dm%dccp%xsz(3)), intent (in) :: uz
  !   real(WP), dimension(4,              dm%dpcc%xsz(2), dm%dpcc%xsz(3)), intent (inout) :: fbcx1
  !   real(WP), dimension(4,              dm%dcpc%xsz(2), dm%dcpc%xsz(3)), intent (inout) :: fbcx2
  !   real(WP), dimension(4,              dm%dccp%xsz(2), dm%dccp%xsz(3)), intent (inout) :: fbcx3

  !   if( .not. dm%is_conv_outlet) return

  !   ! x - pencil 
  !   if(dm%is_conv_outlet(1)) then
  !     !fbcx1(2, :, :) = ux(dm%dpcc%xsz(1), :, :)
  !     fbcx1(4, :, :) = fbcx1(2, :, :)
  !   end if
  !   if(dm%ibcx_nominal(2, 2) == IBC_CONVECTIVE) then
  !     fbcx2(4, :, :) = TWO * fbcx2(2, :, :) - uy(dm%dcpc%xsz(1), :, :)
  !   end if
  !   if(dm%ibcx_nominal(2, 3) == IBC_CONVECTIVE) then
  !     fbcx3(4, :, :) = TWO * fbcx3(2, :, :) - uz(dm%dccp%xsz(1), :, :)
  !   end if

  !   return
  ! end subroutine


!==========================================================================================================
  ! subroutine update_flow_from_dyn_fbcx(dm, ux, uy, uz, fbcx1, fbcx2, fbcx3)
  !   use udf_type_mod
  !   use parameters_constant_mod
  !   use print_msg_mod
  !   implicit none 
  !   type(t_domain), intent(in) :: dm
  !   real(WP), dimension(dm%dpcc%xsz(1), dm%dpcc%xsz(2), dm%dpcc%xsz(3)), intent (inout) :: ux
  !   real(WP), dimension(dm%dcpc%xsz(1), dm%dcpc%xsz(2), dm%dcpc%xsz(3)), intent (inout) :: uy
  !   real(WP), dimension(dm%dccp%xsz(1), dm%dccp%xsz(2), dm%dccp%xsz(3)), intent (inout) :: uz
  !   real(WP), dimension(4,              dm%dpcc%xsz(2), dm%dpcc%xsz(3)), intent (in)    :: fbcx1
  !   real(WP), dimension(4,              dm%dcpc%xsz(2), dm%dcpc%xsz(3)), intent (in)    :: fbcx2
  !   real(WP), dimension(4,              dm%dccp%xsz(2), dm%dccp%xsz(3)), intent (in)    :: fbcx3

  !   if( .not. dm%is_conv_outlet) return

  !   ! x - pencil 
  !   if(dm%is_conv_outlet(1)) then
  !     ux(dm%dpcc%xsz(1), :, :) = fbcx1(2, :, :)
  !   end if
  !   if(dm%ibcx_nominal(2, 2) == IBC_CONVECTIVE) then
  !     uy(dm%dcpc%xsz(1), :, :) = TWO * fbcx2(2, :, :) - fbcx2(4, :, :)
  !   end if
  !   if(dm%ibcx_nominal(2, 3) == IBC_CONVECTIVE) then
  !     uz(dm%dccp%xsz(1), :, :) =  TWO * fbcx3(2, :, :) - fbcx3(4, :, :)
  !   end if
    

  !   return
  ! end subroutine
  !==========================================================================================================
  subroutine enforce_domain_mass_balance_dyn_fbc(drhodt, dm)
    use wtformat_mod
    use bc_dirichlet_mod
    use find_max_min_ave_mod
    use cylindrical_rn_mod
    use solver_tools_mod
    implicit none
    real(WP), dimension(:,:,:), intent(in) :: drhodt
    type(t_domain), intent(inout) :: dm
    type(DECOMP_INFO) :: dtmp
    real(WP) :: scale, mass_imbalance(8)
    real(WP) :: rsign(3)

    integer :: i, j, k
    integer :: nx, ny, nz

    ! only 1 direction could be convective outlet
    if(.not. dm%is_conv_outlet(1) .and. &
       .not. dm%is_conv_outlet(3)) return
    if(dm%is_conv_outlet(2)) call Print_warning_msg('is_conv_outlet=y is not supported')
    ! check mass im-balance
    call check_global_mass_balance(mass_imbalance, drhodt, dm)

    ! vsum(rho) + sum(rhou_xi * Syz) -   sum(rhou_xo * Syz) + 
    !             sum(rhou_yi * Sxz) -   sum(rhou_yo * Sxz) + 
    !             sum(rhou_zi * Sxy) -   sum(rhou_zo * Sxy) = R
    ! vsum(rho) + sum(rhou_xi * Syz) - A*sum(rhou_xo * Syz) + 
    !             sum(rhou_yi * Sxz) - A*sum(rhou_yo * Sxz) + 
    !             sum(rhou_zi * Sxy) - A*sum(rhou_zo * Sxy) = 0
    ! A = 1+R/(Foux'+Fouy'+Fouz')
    rsign(:) = ZERO
    if(dm%is_conv_outlet(1)) rsign(1) = ONE
    if(dm%is_conv_outlet(2)) rsign(2) = ONE
    if(dm%is_conv_outlet(3)) rsign(3) = ONE
    scale = ONE + mass_imbalance(8)/ &
            (mass_imbalance(2)*rsign(1) + &
             mass_imbalance(4)*rsign(2) + &
             mass_imbalance(6)*rsign(3))
#ifdef DEBUG_STEPS
    if(nrank == 0) then
      write(*,*) 'global mass imbalance before correction', mass_imbalance(8)
      write(*,'(9ES18.9)') scale, mass_imbalance(1:8)
    end if
#endif
    ! scale qx/gx convective b.c. 
    if(dm%is_conv_outlet(1)) then
      ny = dm%dpcc%xsz(2); nz = dm%dpcc%xsz(3)
      if(dm%is_thermo) then
        !$acc parallel loop collapse(2) default(present)
        do k = 1, nz; do j = 1, ny
          dm%fbcx_gx(2, j, k) = dm%fbcx_gx(2, j, k) * scale
          dm%fbcx_qx(2, j, k) = dm%fbcx_gx(2, j, k) / dm%fbcx_ftp(2, j, k)%d
        end do; end do
        !$acc end parallel loop
      else
        !$acc parallel loop collapse(2) default(present)
        do k = 1, nz; do j = 1, ny
          dm%fbcx_qx(2, j, k) = dm%fbcx_qx(2, j, k) * scale
        end do; end do
        !$acc end parallel loop
      end if
    end if

    ! scale qz/gz convective b.c. 
    if(dm%is_conv_outlet(3)) then
      nx = dm%dccp%zsz(1); ny = dm%dccp%zsz(2)
      if(dm%is_thermo) then
        !$acc parallel loop collapse(2) default(present)
        do j = 1, ny; do i = 1, nx
          dm%fbcz_gz(i, j, 2) = dm%fbcz_gz(i, j, 2) * scale
          dm%fbcz_qz(i, j, 2) = dm%fbcz_gz(i, j, 2) / dm%fbcz_ftp(i, j, 2)%d
        end do; end do
        !$acc end parallel loop
      else
        !$acc parallel loop collapse(2) default(present)
        do j = 1, ny; do i = 1, nx
          dm%fbcz_qz(i, j, 2) = dm%fbcz_qz(i, j, 2) * scale
        end do; end do
        !$acc end parallel loop
      end if
    end if
    ! double check
#ifdef DEBUG_STEPS
    call check_global_mass_balance(mass_imbalance, drhodt, dm)
    if(nrank == 0) then
      write(*,*) 'global mass imbalance after correction', mass_imbalance(8)
      write(*,'(9ES18.9)') scale, mass_imbalance(1:8)
    end if
#endif

    return
  end subroutine enforce_domain_mass_balance_dyn_fbc

!==========================================================================================================
  subroutine update_fbcx_convective_outlet_flow(fl, dm, isub)
    use bc_dirichlet_mod
    use convert_primary_conservative_mod
    implicit none
    ! arguments
    type(t_flow),   intent(inout) :: fl
    type(t_domain), intent(inout) :: dm
    integer,        intent(in)    :: isub
    ! local variables
    real(WP), dimension(dm%dpcc%xsz(2), dm%dpcc%xsz(3)) :: a0cc
    real(WP), dimension(dm%dcpc%xsz(2), dm%dcpc%xsz(3)) :: a0pc
    real(WP), dimension(dm%dccp%xsz(2), dm%dccp%xsz(3)) :: a0cp
    real(WP), dimension(4, dm%dpcc%xsz(2), dm%dpcc%xsz(3)) :: a4cc
    real(WP), dimension(4, dm%dcpc%xsz(2), dm%dcpc%xsz(3)) :: a4pc
    real(WP), dimension(4, dm%dccp%xsz(2), dm%dccp%xsz(3)) :: a4cp
    real(WP) :: uxdx

    ! condition + default x-pencil
    if(.not. dm%is_conv_outlet(1)) return
    ! get u_c/dx
    call get_convective_outlet_velocity(dm, uxdx)
    ! assign proper variables to calculate convective bc.
    !$acc data create(a0cc, a0pc, a0cp)
    if ( .not. dm%is_thermo) then
      !$acc kernels default(present)
      a0cc(:, :) = fl%qx(dm%dpcc%xsz(1)-1, :, :) ! qx at np-1
      a0pc(:, :) = fl%qy(dm%dcpc%xsz(1),   :, :) ! qy at nc
      a0cp(:, :) = fl%qz(dm%dccp%xsz(1),   :, :) ! qz at nc
      !$acc end kernels
      ! update b.c.
      ! NOTE: passing a scalar expression - uxdx*TWO - is usually safe in OpenACC
      call calculate_fbcx_convective_outlet(dm%fbcx_qx, uxdx*TWO, fl%fbcx_a0cc_rhs0, a0cc, dm, isub)
      call calculate_fbcx_convective_outlet(dm%fbcx_qy, uxdx,     fl%fbcx_a0pc_rhs0, a0pc, dm, isub)
      call calculate_fbcx_convective_outlet(dm%fbcx_qz, uxdx,     fl%fbcx_a0cp_rhs0, a0cp, dm, isub)
      !$acc kernels default(present)
      fl%qx(dm%dpcc%xsz(1), :, :) = dm%fbcx_qx(2, :, :)
      !$acc end kernels
    else
      !$acc kernels default(present)
      a0cc(:, :) = fl%gx(dm%dpcc%xsz(1)-1, :, :)
      a0pc(:, :) = fl%gy(dm%dcpc%xsz(1),   :, :)
      a0cp(:, :) = fl%gz(dm%dccp%xsz(1),   :, :)
      !$acc end kernels
      ! update b.c.
      call calculate_fbcx_convective_outlet(dm%fbcx_gx, uxdx*TWO, fl%fbcx_a0cc_rhs0, a0cc, dm, isub)
      call calculate_fbcx_convective_outlet(dm%fbcx_gy, uxdx,     fl%fbcx_a0pc_rhs0, a0pc, dm, isub)
      call calculate_fbcx_convective_outlet(dm%fbcx_gz, uxdx,     fl%fbcx_a0cp_rhs0, a0cp, dm, isub)
      !$acc kernels default(present)
      fl%gx(dm%dpcc%xsz(1), :, :) = dm%fbcx_gx(2, :, :)
      !$acc end kernels
    end if
    !$acc end data

    return
  end subroutine
!==========================================================================================================
  subroutine update_fbcz_convective_outlet_flow(fl, dm, isub)
    use bc_dirichlet_mod
    use convert_primary_conservative_mod
    use transpose_extended_mod
    implicit none
    ! arguments
    type(t_flow),   intent(inout) :: fl
    type(t_domain), intent(inout) :: dm
    integer,        intent(in)    :: isub
    ! local variables
    real(WP), dimension(dm%dpcc%zsz(1), dm%dpcc%zsz(2)) :: acc0
    real(WP), dimension(dm%dcpc%zsz(1), dm%dcpc%zsz(2)) :: apc0
    real(WP), dimension(dm%dccp%zsz(1), dm%dccp%zsz(2)) :: acp0
    real(WP), pointer, dimension(:,:,:) :: apcc_zpencil
    real(WP), pointer, dimension(:,:,:) :: acpc_zpencil
    real(WP), pointer, dimension(:,:,:) :: accp_zpencil
    integer, dimension(3) :: npccz, ncpcz, nccpz
    real(WP) :: uzdz

    ! condition
    if(.not. dm%is_conv_outlet(3)) return
    npccz = dm%dpcc%zsz
    ncpcz = dm%dcpc%zsz
    nccpz = dm%dccp%zsz
    apcc_zpencil(1:npccz(1),1:npccz(2),1:npccz(3)) => fl%wk1
    acpc_zpencil(1:ncpcz(1),1:ncpcz(2),1:ncpcz(3)) => fl%wk2
    accp_zpencil(1:nccpz(1),1:nccpz(2),1:nccpz(3)) => fl%wk3
    ! get u_c/dz
    call get_convective_outlet_velocity(dm, uzdz)
    ! assign proper variables to calculate convective bc.
    !$acc data create(apc0, acp0, acc0)
    if ( .not. dm%is_thermo) then
      call transpose_to_z_pencil(fl%qx, apcc_zpencil, dm%dpcc, IPENCIL(1))
      call transpose_to_z_pencil(fl%qy, acpc_zpencil, dm%dcpc, IPENCIL(1))
      call transpose_to_z_pencil(fl%qz, accp_zpencil, dm%dccp, IPENCIL(1))
      ! TODO: data is contiguous in memory, copy is not needed
      !$acc kernels default(present)
      apc0 = apcc_zpencil(:, :, dm%dpcc%zsz(3)  )
      acp0 = acpc_zpencil(:, :, dm%dcpc%zsz(3)  )
      acc0 = accp_zpencil(:, :, dm%dccp%zsz(3)-1)
      !$acc end kernels
      ! update b.c.
      call calculate_fbcz_convective_outlet(dm%fbcz_qx, uzdz,     fl%fbcz_apc0_rhs0, apc0, dm, isub)
      call calculate_fbcz_convective_outlet(dm%fbcz_qy, uzdz,     fl%fbcz_acp0_rhs0, acp0, dm, isub)
      call calculate_fbcz_convective_outlet(dm%fbcz_qz, uzdz*TWO, fl%fbcz_acc0_rhs0, acc0, dm, isub)
      !$acc kernels default(present)
      accp_zpencil(:, :, 1) = dm%fbcz_qz(:, :, 2)
      !$acc end kernels
      call transpose_from_z_pencil(accp_zpencil, fl%qz, dm%dccp, IPENCIL(1))
    else
      call transpose_to_z_pencil(fl%gx, apcc_zpencil, dm%dpcc, IPENCIL(1))
      call transpose_to_z_pencil(fl%gy, acpc_zpencil, dm%dcpc, IPENCIL(1))
      call transpose_to_z_pencil(fl%gz, accp_zpencil, dm%dccp, IPENCIL(1))
      ! TODO: data is contiguous in memory, copy is not needed
      !$acc kernels default(present)
      apc0 = apcc_zpencil(:, :, dm%dpcc%zsz(3)  )
      acp0 = acpc_zpencil(:, :, dm%dcpc%zsz(3)  )
      acc0 = accp_zpencil(:, :, dm%dccp%zsz(3)-1)
      !$acc end kernels
      ! update b.c.
      call calculate_fbcz_convective_outlet(dm%fbcz_gx, uzdz,     fl%fbcz_apc0_rhs0, apc0, dm, isub)
      call calculate_fbcz_convective_outlet(dm%fbcz_gy, uzdz,     fl%fbcz_acp0_rhs0, acp0, dm, isub)
      call calculate_fbcz_convective_outlet(dm%fbcz_gz, uzdz*TWO, fl%fbcz_acc0_rhs0, acc0, dm, isub)
      !$acc kernels default(present)
      accp_zpencil(:, :, dm%dccp%zsz(3)) = dm%fbcz_gz(:, :, 2)
      !$acc end kernels
      call transpose_from_z_pencil(accp_zpencil, fl%gz, dm%dccp, IPENCIL(1))
    end if
    !$acc end data

    return
  end subroutine
!==========================================================================================================
  subroutine update_convective_outlet_flow(fl, dm, isub)
    use convert_primary_conservative_mod
    use bc_dirichlet_mod
    implicit none
    ! arguments
    type(t_flow),   intent(inout) :: fl
    type(t_domain), intent(inout) :: dm
    integer,        intent(in)    :: isub

    if(.not. dm%is_conv_outlet(1) .and. &
      .not. dm%is_conv_outlet(3)) return

    if(dm%is_conv_outlet(1)) call update_fbcx_convective_outlet_flow(fl, dm, isub)
    if(dm%is_conv_outlet(3)) call update_fbcz_convective_outlet_flow(fl, dm, isub)

    call enforce_domain_mass_balance_dyn_fbc(fl%drhodt, dm)

    if ( .not. dm%is_thermo) then
      call enforce_velo_from_fbc(dm, fl%qx, fl%qy, fl%qz, dm%fbcx_qx, dm%fbcy_qy, dm%fbcz_qz)
    else
      call enforce_velo_from_fbc(dm, fl%gx, fl%gy, fl%gz, dm%fbcx_gx, dm%fbcy_gy, dm%fbcz_gz)
      call convert_primary_conservative(fl, dm, fl%dDens, IG2Q, IBND)
    end if

    return
  end subroutine
!==========================================================================================================
  subroutine update_fbcx_convective_outlet_thermo(tm, dm, isub)
    use thermo_info_mod
    implicit none
    ! arguments
    type(t_thermo), intent(inout) :: tm
    type(t_domain), intent(inout) :: dm
    integer,        intent(in)    :: isub
    ! local variables
    real(WP) :: uxdx
    integer :: j, k
    integer :: ny, nz
    real(WP), dimension(4, dm%dccc%xsz(2), dm%dccc%xsz(3)) :: a4cc_xpencil
    real(WP), dimension(   dm%dccc%xsz(2), dm%dccc%xsz(3)) :: a0cc_xpencil

    ! conditions
    if ( .not. dm%is_thermo) return
    if ( .not. dm%is_conv_outlet(1)) return
    if ( dm%ibcx_nominal(2, 5) /= IBC_CONVECTIVE) return
    ! get u_c/dx
    call get_convective_outlet_velocity(dm, uxdx)
    ! assign proper variables to calculate convective bc.
    !$acc data create(a4cc_xpencil, a0cc_xpencil)
    !$acc kernels default(present)
    a4cc_xpencil(:, :, :) = dm%fbcx_ftp(:, :, :)%rhoh
    a0cc_xpencil(:, :)    = tm%rhoh(dm%dccc%xsz(1), :, :)
    !$acc end kernels
    ! update b.c.
    call calculate_fbcx_convective_outlet(a4cc_xpencil, uxdx, tm%fbcx_rhoh_rhs0, a0cc_xpencil, dm, isub)
    !call enforce_domain_energy_balance_dyn_fbc(fl, dm) ! todo-check necessary?
    ! update other properties
    !$acc kernels default(present)
    dm%fbcx_ftp(:, :, :)%rhoh = a4cc_xpencil(:, :, :)
    !$acc end kernels

    ny = size(dm%fbcx_ftp, 2)
    nz = size(dm%fbcx_ftp, 3)
    !$acc parallel loop collapse(2) default(present)
    do k = 1, nz
      do j = 1, ny
        call ftp_refresh_thermal_properties_from_DH(dm%fbcx_ftp(2, j, k))
        call ftp_refresh_thermal_properties_from_DH(dm%fbcx_ftp(4, j, k))
      end do
    end do
    !$acc end parallel loop
    !$acc end data

    return
  end subroutine

  !==========================================================================================================
  subroutine update_fbcz_convective_outlet_thermo(tm, dm, isub)
    use thermo_info_mod
    use transpose_extended_mod
    implicit none
    ! arguments
    type(t_thermo), intent(inout) :: tm
    type(t_domain), intent(inout) :: dm
    integer,        intent(in)    :: isub
    ! local variables
    real(WP) :: uzdz
    integer :: i, j
    integer :: nx, ny
    real(WP), dimension(dm%dccc%zsz(1), dm%dccc%zsz(2), 4)              :: acc4_zpencil
    real(WP), dimension(dm%dccc%zsz(1), dm%dccc%zsz(2), dm%dccc%zsz(3)) :: accc_zpencil
    real(WP), dimension(dm%dccc%zsz(1), dm%dccc%zsz(2))                 :: acc0_zpencil

    ! condition
    if ( .not. dm%is_thermo) return
    if ( .not. dm%is_conv_outlet(3)) return
    if ( dm%ibcz_nominal(2, 5) /= IBC_CONVECTIVE) return
    ! get u_c/dz or u_c/d_theta
    call get_convective_outlet_velocity(dm, uzdz)
    !$acc data create(acc4_zpencil, acc0_zpencil, accc_zpencil)
    ! assign proper variables to calculate convective bc.
    call transpose_to_z_pencil(tm%rhoh, accc_zpencil, dm%dccc, IPENCIL(1))
    !$acc kernels default(present)
    acc0_zpencil(:, :)    = accc_zpencil(:, :, dm%dccc%zsz(3))
    acc4_zpencil(:, :, :) = dm%fbcz_ftp(:, :, :)%rhoh
    !$acc end kernels
    ! update b.c.
    call calculate_fbcz_convective_outlet(acc4_zpencil, uzdz, tm%fbcz_rhoh_rhs0, acc0_zpencil, dm, isub)
    !!call enforce_domain_energy_balance_dyn_fbc(fl, dm) ! todo-check necessary? 
    ! update other properties
    !$acc kernels default(present)
    dm%fbcz_ftp(:, :, :)%rhoh = acc4_zpencil(:, :, :)
    !$acc end kernels
    nx = size(dm%fbcz_ftp, 1)
    ny = size(dm%fbcz_ftp, 2)
    !$acc parallel loop collapse(2) default(present)
    do j = 1, ny
      do i = 1, nx
        call ftp_refresh_thermal_properties_from_DH(dm%fbcz_ftp(i, j, 2))
        call ftp_refresh_thermal_properties_from_DH(dm%fbcz_ftp(i, j, 4))
      end do
    end do
    !$acc end parallel loop
    !$acc end data

    return
  end subroutine

  !==========================================================================================================
  subroutine update_convective_outlet_thermo(tm, dm, isub)
    use udf_type_mod
    implicit none
    ! arguments
    type(t_thermo), intent(inout) :: tm
    type(t_domain), intent(inout) :: dm
    integer,        intent(in)    :: isub

    if(.not. dm%is_conv_outlet(1) .and. &
       .not. dm%is_conv_outlet(3)) return

    if (dm%is_conv_outlet(1)) then 
      call update_fbcx_convective_outlet_thermo(tm, dm, isub)
    end if
    if (dm%is_conv_outlet(3)) then 
      call update_fbcz_convective_outlet_thermo(tm, dm, isub)
    end if

    return
  end subroutine

end module
